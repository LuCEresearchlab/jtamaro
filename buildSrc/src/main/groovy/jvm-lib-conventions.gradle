plugins {
  id("java-library")
}

group = rootProject.group
version = rootProject.version

repositories {
  mavenCentral()
}

java {
  withJavadocJar()
  withSourcesJar()

  toolchain.languageVersion.set(JavaLanguageVersion.of(21))
}

javadoc {
  configure(options) {
    memberLevel = JavadocMemberLevel.PUBLIC
    addStringOption("Xdoclint:all,-missing", "-quiet")
    addBooleanOption("html5", true)
    tags(
        "apiNote:a:API Note:",
        "implSpec:a:Implementation Requirements:",
        "implNote:a:Implementation Note:",
    )
  }
}

jar {
  manifest {
    attributes([
        "Implementation-Title"  : project.name,
        "Implementation-Version": archiveVersion,
    ])
  }
}

compileJava {
  configure(options) {
    compilerArgs << "-Xlint:all,-serial"
  }
}
